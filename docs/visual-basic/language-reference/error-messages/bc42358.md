---
title: Protože se toto volání neočekává, vykonávání aktuální metody pokračuje před dokončením volání.
ms.date: 07/20/2015
f1_keywords:
- bc42358
- vbc42358
helpviewer_keywords:
- BC42358
ms.assetid: 43342515-c3c8-4155-9263-c302afabcbc2
ms.openlocfilehash: 20a12071ccecf119059e2dfd7ddb02b2ec2e272c
ms.sourcegitcommit: f8c270376ed905f6a8896ce0fe25b4f4b38ff498
ms.translationtype: MT
ms.contentlocale: cs-CZ
ms.lasthandoff: 06/04/2020
ms.locfileid: "84415437"
---
# <a name="because-this-call-is-not-awaited-execution-of-the-current-method-continues-before-the-call-is-completed"></a>Protože se toto volání neočekává, vykonávání aktuální metody pokračuje před dokončením volání.

Protože toto volání není očekáváno, vykonání aktuální metody pokračuje před dokončením volání. Zvažte použití `Await` operátoru na výsledek volání.

Aktuální metoda volá asynchronní metodu, která vrátí <xref:System.Threading.Tasks.Task> nebo <xref:System.Threading.Tasks.Task%601> a a nepoužije operátor [await](../operators/await-operator.md) na výsledek. Volání asynchronní metody spustí asynchronní úlohu. Protože však `Await` není použit žádný operátor, program pokračuje bez čekání na dokončení úlohy. Ve většině případů se toto chování neočekává. Obvykle jiné aspekty volající metody závisí na výsledcích volání nebo, je-li metoda volaná, je očekávána před návratem z metody, která obsahuje volání.

Stejně důležitý problém je to, co se stane s výjimkami, které jsou vyvolány v volané asynchronní metodě. Výjimka, která je vyvolána v metodě, která vrací <xref:System.Threading.Tasks.Task> nebo <xref:System.Threading.Tasks.Task%601> je uložena v vráceném úkolu. Pokud neočekáváte úlohu nebo explicitně kontrolujete výjimky, výjimka se ztratí. Pokud očekáváte úlohu, její výjimka se znovu vyvolá.

V souladu s osvědčeným postupem byste měli vždy očekávat volání.

Ve výchozím nastavení je tato zpráva upozornění. Další informace o skrývání upozornění nebo zpracování upozornění jako chyb najdete v tématu [Konfigurace upozornění v Visual Basic](/visualstudio/ide/configuring-warnings-in-visual-basic).

**ID chyby:** BC42358

## <a name="to-address-this-warning"></a>Řešení tohoto upozornění

Upozornění byste měli potlačit jenom v případě, že si jste jistí, že nechcete čekat na dokončení asynchronního volání a že volaná metoda nevyvolá žádné výjimky. V takovém případě můžete upozornění potlačit přiřazením výsledku úlohy volání proměnné.

Následující příklad ukazuje, jak vyvolat upozornění, jak ho potlačit a jak očekávat volání:

```vb
Async Function CallingMethodAsync() As Task

    ResultsTextBox.Text &= vbCrLf & "  Entering calling method."

    ' Variable delay is used to slow down the called method so that you
    ' can distinguish between awaiting and not awaiting in the program's output.
    ' You can adjust the value to produce the output that this topic shows
    ' after the code.
    Dim delay = 5000

    ' Call #1.
    ' Call an async method. Because you don't await it, its completion isn't
    ' coordinated with the current method, CallingMethodAsync.
    ' The following line causes the warning.
    CalledMethodAsync(delay)

    ' Call #2.
    ' To suppress the warning without awaiting, you can assign the
    ' returned task to a variable. The assignment doesn't change how
    ' the program runs. However, the recommended practice is always to
    ' await a call to an async method.
    ' Replace Call #1 with the following line.
    'Task delayTask = CalledMethodAsync(delay)

    ' Call #3
    ' To contrast with an awaited call, replace the unawaited call
    ' (Call #1 or Call #2) with the following awaited call. The best
    ' practice is to await the call.

    'Await CalledMethodAsync(delay)

    ' If the call to CalledMethodAsync isn't awaited, CallingMethodAsync
    ' continues to run and, in this example, finishes its work and returns
    ' to its caller.
    ResultsTextBox.Text &= vbCrLf & "  Returning from calling method."
End Function

Async Function CalledMethodAsync(howLong As Integer) As Task

    ResultsTextBox.Text &= vbCrLf & "    Entering called method, starting and awaiting Task.Delay."
    ' Slow the process down a little so you can distinguish between awaiting
    ' and not awaiting. Adjust the value for howLong if necessary.
    Await Task.Delay(howLong)
    ResultsTextBox.Text &= vbCrLf & "    Task.Delay is finished--returning from called method."
End Function
```

Pokud v příkladu zvolíte volání #1 nebo volání #2, neočekávaná asynchronní metoda ( `CalledMethodAsync` ) se dokončí po jeho volajícím ( `CallingMethodAsync` ) a volajícím ( `StartButton_Click` ). Poslední řádek v následujícím výstupu ukazuje, kdy se volaná metoda dokončí. Vstup a výstup z obslužné rutiny události, která volá `CallingMethodAsync` v úplném příkladu, je označen ve výstupu.

```console
Entering the Click event handler.
  Entering calling method.
    Entering called method, starting and awaiting Task.Delay.
  Returning from calling method.
Exiting the Click event handler.
    Task.Delay is finished--returning from called method.
```

## <a name="example"></a>Příklad

Následující aplikace Windows Presentation Foundation (WPF) obsahuje metody z předchozího příkladu. V následujících krocích nastavíte aplikaci:

1. Vytvořte aplikaci WPF a pojmenujte ji `AsyncWarning` .

2. V editoru Visual Studio Code klikněte na kartu **MainWindow. XAML** .

     Pokud karta není viditelná, otevřete místní nabídku pro MainWindow. XAML v **Průzkumník řešení**a pak zvolte **Zobrazit kód**.

3. Nahraďte kód v zobrazení **XAML** souboru MainWindow. XAML následujícím kódem:

    ```xaml
    <Window x:Class="MainWindow"
            xmlns="http://schemas.microsoft.com/winfx/2006/xaml/presentation"
            xmlns:x="http://schemas.microsoft.com/winfx/2006/xaml"
            Title="MainWindow" Height="350" Width="525">
        <Grid>
            <Button x:Name="StartButton" Content="Start" HorizontalAlignment="Left" Margin="214,28,0,0" VerticalAlignment="Top" Width="75" HorizontalContentAlignment="Center" FontWeight="Bold" FontFamily="Aharoni" Click="StartButton_Click" />
            <TextBox x:Name="ResultsTextBox" Margin="0,80,0,0" TextWrapping="Wrap" FontFamily="Lucida Console"/>
        </Grid>
    </Window>
    ```

     Jednoduché okno obsahující tlačítko a textové pole se zobrazí v zobrazení **Návrh** souboru MainWindow. XAML.

     Další informace o Návrhář XAML najdete v tématu [Vytvoření uživatelského rozhraní pomocí Návrhář XAML](/visualstudio/xaml-tools/creating-a-ui-by-using-xaml-designer-in-visual-studio). Informace o tom, jak vytvořit vlastní jednoduché uživatelské rozhraní, naleznete v části "Vytvoření aplikace WPF" a "Návrh jednoduchého MainWindow WPF" v tématu [Postupy: přístup k webu pomocí modifikátoru Async a operátoru await](../../programming-guide/concepts/async/walkthrough-accessing-the-web-by-using-async-and-await.md).

4. Nahraďte kód v souboru MainWindow. XAML. vb následujícím kódem.

    ```vb
    Class MainWindow

        Private Async Sub StartButton_Click(sender As Object, e As RoutedEventArgs)

            ResultsTextBox.Text &= vbCrLf & "Entering the Click event handler."
            Await CallingMethodAsync()
            ResultsTextBox.Text &= vbCrLf & "Exiting the Click event handler."
        End Sub

        Async Function CallingMethodAsync() As Task

            ResultsTextBox.Text &= vbCrLf & "  Entering calling method."

            ' Variable delay is used to slow down the called method so that you
            ' can distinguish between awaiting and not awaiting in the program's output.
            ' You can adjust the value to produce the output that this topic shows
            ' after the code.
            Dim delay = 5000

            ' Call #1.
            ' Call an async method. Because you don't await it, its completion isn't
            ' coordinated with the current method, CallingMethodAsync.
            ' The following line causes the warning.
            CalledMethodAsync(delay)

            ' Call #2.
            ' To suppress the warning without awaiting, you can assign the
            ' returned task to a variable. The assignment doesn't change how
            ' the program runs. However, the recommended practice is always to
            ' await a call to an async method.

            ' Replace Call #1 with the following line.
            'Task delayTask = CalledMethodAsync(delay)

            ' Call #3
            ' To contrast with an awaited call, replace the unawaited call
            ' (Call #1 or Call #2) with the following awaited call. The best
            ' practice is to await the call.

            'Await CalledMethodAsync(delay)

            ' If the call to CalledMethodAsync isn't awaited, CallingMethodAsync
            ' continues to run and, in this example, finishes its work and returns
            ' to its caller.
            ResultsTextBox.Text &= vbCrLf & "  Returning from calling method."
        End Function

        Async Function CalledMethodAsync(howLong As Integer) As Task

            ResultsTextBox.Text &= vbCrLf & "    Entering called method, starting and awaiting Task.Delay."
            ' Slow the process down a little so you can distinguish between awaiting
            ' and not awaiting. Adjust the value for howLong if necessary.
            Await Task.Delay(howLong)
            ResultsTextBox.Text &= vbCrLf & "    Task.Delay is finished--returning from called method."
        End Function

    End Class

    ' Output

    ' Entering the Click event handler.
    '   Entering calling method.
    '     Entering called method, starting and awaiting Task.Delay.
    '   Returning from calling method.
    ' Exiting the Click event handler.
    '     Task.Delay is finished--returning from called method.

    ' Output

    ' Entering the Click event handler.
    '   Entering calling method.
    '     Entering called method, starting and awaiting Task.Delay.
    '     Task.Delay is finished--returning from called method.
    '   Returning from calling method.
    ' Exiting the Click event handler.
    ```

5. Zvolte klávesu F5 ke spuštění programu a pak klikněte na tlačítko **Start** .

     Očekávaný výstup se zobrazí na konci kódu.

## <a name="see-also"></a>Viz také

- [Await – operátor](../operators/await-operator.md)
- [Asynchronní programování s modifikátorem Async a operátoru await](../../programming-guide/concepts/async/index.md)
